/*
 * [The "BSD licence"]
 *  Copyright (c) 2017 David J Hait
 *  All rights reserved.
 *
 *  Redistribution and use in source and binary forms, with or without
 *  modification, are permitted provided that the following conditions
 *  are met:
 *  1. Redistributions of source code must retain the above copyright
 *     notice, this list of conditions and the following disclaimer.
 *  2. Redistributions in binary form must reproduce the above copyright
 *     notice, this list of conditions and the following disclaimer in the
 *     documentation and/or other materials provided with the distribution.
 *  3. The name of the author may not be used to endorse or promote products
 *     derived from this software without specific prior written permission.
 *
 *  THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 *  IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 *  OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
 *  IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT,
 *  INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT
 *  NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 *  DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 *  THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 *  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF
 *  THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

/* This is an example of the birthday book. */

\begin{zsection}
\SECTION birthday
\end{zsection}

\begin{zed}
 [ NAME, DATE ]
\end{zed}

\begin{schema}{BirthdayBook}
    known : \power NAME
    birthday : NAME \pfun DATE
\where
    known = dom birthday
\end{schema}

\begin{schema}{AddBirthday}
    \Delta BirthdayBook
    name? : NAME
    date? : DATE
\where
    name? \notin known
    birthday′ = birthday \cup { name? \mapsto date? }
\end{schema}

Now we can put some informal text in the middle.

\begin{schema}{FindBirthday}
    \Xi BirthdayBook
    name? : NAME
    date! : DATE
\where
    name? \in known
    date! = birthday ( name? )
\end{schema}

\begin{schema}{Remind}
	\Xi BirthdayBook
    today? : DATE
    cards! : \power NAME
\where
    cards! = { n : known | birthday ( n ) = today? }
\end{schema}

\begin{schema}{InitBirthdayBook}
    BirthdayBook′
\where
    known′ = { }
\end{schema}

\begin{zed}
   REPORT ::= ok | already_known | not_known
\end{zed}

\begin{schema}{Success}
    result! : REPORT
\where
    result! = ok
\end{schema}

\begin{schema}{AlreadyKnown}
    \Xi BirthdayBook
    name? : NAME
    result! : REPORT
\where
    name? \in known
    result! = already_known
\end{schema}

\begin{zed}
    RAddBirthday == ( AddBirthday \land Success ) \lor AlreadyKnown
\end{zed}

\begin{schema}{NotKnown}
    \Xi BirthdayBook
    name? : NAME
    result! : REPORT
\where
    name? \notin known
    result! = not_known
\end{schema}

\begin{zed}
    RFindBirthday == ( FindBirthday \land Success ) \lor NotKnown
\end{zed}

\begin{zed}
    RRemind == Remind \land Success
\end{zed}
